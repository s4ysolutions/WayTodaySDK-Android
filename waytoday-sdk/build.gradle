plugins {
    id 'com.android.library'
    id 'com.google.protobuf' version '0.9.4'
    id 'maven-publish'
    // id 'signing'
}

android {
    namespace = 'solutions.s4y.waytoday.sdk'
    buildFeatures {
        buildConfig true
    }
}

ext {
    releaseVersion = System.getenv('RELEASE_VERSION') ?: '1.0.33'
}

group 'solutions.s4y.waytoday.sdk'
version releaseVersion

android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 19
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "int", "GRPC_PORT", "9101"
            buildConfigField "int", "GRPC_PORT_PLAIN", "9001"
            buildConfigField "String", "GRPC_HOST", "\"tracker.way.today\""
        }
        release {
            minifyEnabled false
            buildConfigField "int", "GRPC_PORT", "9101"
            buildConfigField "int", "GRPC_PORT_PLAIN", "9001"
            buildConfigField "String", "GRPC_HOST", "\"tracker.way.today\""
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'io.grpc:grpc-protobuf-lite:1.59.0'
    implementation 'io.grpc:grpc-stub:1.59.0'
    implementation 'io.grpc:grpc-okhttp:1.59.0'
    implementation 'com.github.maddevsio:mad-location-manager:0.1.16'

    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'com.google.truth:truth:1.1.5'
    testImplementation 'org.robolectric:robolectric:4.11.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.59.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

task javaSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs

    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        Release(MavenPublication) {
            groupId = 'solutions.s4y.waytoday'
            artifactId = 'waytoday-sdk'
            version = releaseVersion

            pom {
                packaging 'aar'
                name = 'WayToday SDK for Android'
                description = 'Android GPS tracker, WayToday ID and Upload services'
                url = 'https://github.com/s4ysolutions/WayTodaySDK-Android'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/s4ysolutions/WayTodaySDK-Android/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'dsame'
                        name = 'Sergey Dolin'
                        email = 'sergey@s4y.solutions'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/s4ysolutions/WayTodaySDK-Android.git'
                    developerConnection = 'scm:git:ssh://github.com/s4ysolutions/WayTodaySDK-Android.git'
                    url = 'https://github.com/s4ysolutions/WayTodaySDK-Android'
                }
            }

            afterEvaluate {
                from components.release
                //               artifact sourceJar
//                artifact javadocJar
            }
        }
    }
}