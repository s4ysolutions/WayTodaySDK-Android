plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace = 'solutions.s4y.waytoday.sdk'
    buildFeatures {
        buildConfig true
    }

    compileSdk 34

    defaultConfig {
        minSdkVersion 19

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    api "androidx.work:work-runtime:2.9.0"
    api 'com.github.s4ysolutions:GPSAndroidSDK:3.2.0'
    // api "solutions.s4y.gps:gps-sdk-android:3.2.0"
    api 'com.github.s4ysolutions:WayTodaySDK-Java:3.0.0-alpha1'
    //api "solutions.s4y.waytoday:waytoday-sdk-java:3.0.0-alpha1"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'androidx.preference:preference:1.2.1'
}

tasks.register('javaSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs

    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        Release(MavenPublication) {
            groupId = 'solutions.s4y.waytoday'
            artifactId = 'waytoday-sdk-android'
            version = '4.2.0'

            pom {
                packaging 'aar'
                name = 'WayToday SDK for Android'
                description = 'Android GPS tracker, WayToday ID and Upload services'
                url = 'https://github.com/s4ysolutions/WayTodaySDK-Android'
                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://github.com/s4ysolutions/WayTodaySDK-Android/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'dsame'
                        name = 'Sergey Dolin'
                        email = 'sergey@s4y.solutions'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/s4ysolutions/WayTodaySDK-Android.git'
                    developerConnection = 'scm:git:ssh://github.com/s4ysolutions/WayTodaySDK-Android.git'
                    url = 'https://github.com/s4ysolutions/WayTodaySDK-Android'
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}